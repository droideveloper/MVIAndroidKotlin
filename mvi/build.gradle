apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext {
  versionMajor = 3
  versionMinor = 0

  k100 = 100000
  k10 = 10000
  buildNumber = 0
}

android {
  compileSdkVersion 30

  defaultConfig {
    minSdkVersion 16
    versionCode versionCode(versionMajor, versionMinor, buildNumber, k100, k10)
    versionName versionName(versionMajor, versionMinor, buildNumber)
  }

  lintOptions {
    abortOnError false
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    staging {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }
}

androidExtensions {
  experimental = true
}

dependencies {
  // this is for androidx compatibility
  implementation "androidx.appcompat:appcompat:1.2.0"
  implementation "androidx.recyclerview:recyclerview:$AND_X_VER"
  implementation "com.google.android.material:material:$MATER_VER"

  implementation "androidx.viewpager:viewpager:1.0.0"

  implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
  // rxjava3 call adapter
  implementation "com.squareup.retrofit2:adapter-rxjava3:$RETROFIT_VERSION"

  implementation "io.reactivex.rxjava3:rxandroid:$RX_ANDROID_VERSION"
  implementation "io.reactivex.rxjava3:rxkotlin:$RX_KOTLIN_VERSION"
  implementation "com.jakewharton.rxrelay3:rxrelay:$RX_RELAY_KOTLIN"

  implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"

  implementation "com.google.dagger:dagger:$DAGGER_VERSION"
  implementation "com.google.dagger:dagger-android:$DAGGER_VERSION"
  implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"

  kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
  kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

  implementation "androidx.room:room-runtime:$DATABASE_VERSION"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
}

private static def versionName(Integer versionMajor, Integer versionMinor, Integer buildNumber) {
  return String.format("%d.%d.%d", versionMajor, versionMinor, buildNumber)
}

private static def versionCode(Integer versionMajor, Integer versionMinor, Integer buildNumber, Integer k100, Integer k10) {
  return (versionMajor * k100) + (versionMinor * k10) + buildNumber
}

ext {
  PUBLISH_GROUP_ID = 'org.fs.architecture'
  PUBLISH_ARTIFACT_ID = 'mvi'
  PUBLISH_VERSION = versionName(versionMajor, versionMinor, buildNumber)
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'
